PREFIX schema: <https://schema.org/>
PREFIX rdfs:   <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:    <http://www.w3.org/2001/XMLSchema#>
PREFIX ex:     <https://www.roadsafetydata.com/ontology#>

# Q0 — Tipos más frecuentes
SELECT ?type (COUNT(*) AS ?n)
WHERE { ?s a ?type }
GROUP BY ?type
ORDER BY DESC(?n)
LIMIT 20

# Q1 — Accidentes: URI, label y fecha
SELECT ?accidente ?label ?fecha
WHERE {
  ?accidente a schema:Event ;
             rdfs:label ?label ;
             schema:startDate ?fecha .
}
LIMIT 20

# Q2 — Places con dirección y número (si existe)
SELECT ?place ?dir ?num
WHERE {
  ?place a schema:Place ;
         schema:streetAddress ?dir .
  OPTIONAL { ?place schema:streetNumber ?num }
}
LIMIT 20

# Q3 — Participantes: label, género, rol, vehículo, lesividad
SELECT ?person ?label ?gender ?role ?vehicle ?injury
WHERE {
  ?person a schema:Person ;
          rdfs:label ?label .
  OPTIONAL { ?person schema:gender  ?gender }
  OPTIONAL { ?person ex:personRole  ?role }
  OPTIONAL { ?person ex:vehicleType ?vehicle }
  OPTIONAL { ?person ex:injuryLevel ?injury }
}
LIMIT 20

# Q4 — Accidentes y su distrito (con label del distrito si está)
SELECT ?event ?eventLabel ?district ?districtLabel
WHERE {
  ?event a schema:Event ;
         rdfs:label ?eventLabel ;
         schema:areaServed ?district .
  OPTIONAL { ?district rdfs:label ?districtLabel }
}
LIMIT 20

# Q5 — Radares con límite >= 90 km/h
SELECT ?cam ?limit ?lat ?long
WHERE {
  ?cam a ex:SpeedCamera ;
       ex:speedLimit ?limit ;
       schema:latitude  ?lat ;
       schema:longitude ?long .
  FILTER(xsd:integer(?limit) >= 90)
}
ORDER BY DESC(?limit)
LIMIT 20
